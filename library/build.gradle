plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.kotlin.android)
    id "maven-publish"
    id "signing"
}

group = "io.github.reline"
version = "0.1.1-alpha03"

publishing {
    publications {
        release(MavenPublication) {
            artifactId = "sqlitecopyopenhelper"

            pom {
                name = "SQLiteCopyOpenHelper"
                description = "An open helper for Android that will copy & open a pre-populated database"
                url = "https://github.com/reline/SQLiteCopyOpenHelper"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        distribution = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = "reline"
                        name = "Nathan Reline"
                        url = "https://github.com/reline/"
                    }
                }
                scm {
                    url = "https://github.com/reline/SQLiteCopyOpenHelper"
                    connection = "scm:git:git://github.com/reline/SQLiteCopyOpenHelper.git"
                    developerConnection = "scm:git:ssh://git@github.com/reline/SQLiteCopyOpenHelper.git"
                }
            }

            afterEvaluate {
                from components.release
            }
        }
    }
}

signing {
    sign publishing.publications.release
}

android {
    namespace "io.github.reline.sqlite.db"
    compileSdk 34

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 34
        versionCode 1
        versionName version

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    testFixtures {
        enable = true
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

kotlin {
    jvmToolchain(17)
}

dependencies {
    implementation libs.kotlin.stdlib
    implementation libs.okio
    implementation libs.androidx.sqlite

    testImplementation libs.androidx.test.junit
    testImplementation libs.androidx.test.core
    testImplementation libs.robolectric
    testImplementation libs.androidx.sqlite.framework
    testImplementation libs.junit
}
